<HTML>
<HEAD>
<TITLE>AspPDF User Manual Chapter 6: HTML Support Sample</TITLE>
</HEAD>
<BODY>

<%
	Set Pdf = Server.CreateObject("Persits.Pdf")

	Set Doc = Pdf.CreateDocument

	' Small pages - to demonstrate text spanning
	Set page = Doc.Pages.Add( 300, 300 )

	' Use Arial font
	Set Font = Doc.Fonts("Arial")

	' Load string from file
	Text = Pdf.LoadTextFromFile(Server.MapPath("html.txt") )
	
	' Parameters: X, Y of upper-left corner of text box, Height, Width, HTML=true
	Set param = pdf.CreateParam("x=10; y=290; width=280; height=280; html=true")
	
	Do While Len(Text) > 0	
		CharsPrinted = Page.Canvas.DrawText(Text, param, Font )

		' Save HTML tag generated by DrawText to reflect current font state
		HtmlTag = Page.Canvas.HtmlTag

		' We printed the entire string. Exit loop.
		if CharsPrinted = Len(Text) Then Exit Do

		' Otherwise print remaining text on next page
		Set Page = Page.NextPage

		Text = HtmlTag & Right( Text, Len(Text) - CharsPrinted)
	Loop

	' Save document, the Save method returns generated file name
	Filename = Doc.Save( Server.MapPath("html.pdf"), False )

	Response.Write "Success! Download your PDF file <A HREF=" & Filename & ">here</A>"
%>

</BODY>
</HTML>
